plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'java'
    id 'org.asciidoctor.jvm.convert' version '4.0.3'
}

group = 'com.tsinsi'
version = '0.0.1-SNAPSHOT'

ext {
    snippetsDir = file('build/generated-snippets')
}

configurations {
    asciidoctorExt
}

dependencies {
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    implementation project(':scaffold')

    implementation 'com.nimbusds:nimbus-jose-jwt:9.40'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testng:testng:7.10.2'
}

tasks.named('test') {
    useTestNG()
    outputs.dir snippetsDir
}

tasks.named('asciidoctor') {
    configurations "asciidoctorExt"
    inputs.dir snippetsDir
    dependsOn test
    baseDirFollowsSourceDir()
}

bootJar {
    dependsOn asciidoctor

    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}