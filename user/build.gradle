plugins {
    id 'java'
    id 'buildlogic.spring-conventions'
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
}

group = 'com.tsinsi.user'
version = '0.0.1-SNAPSHOT'

ext {
    snippetsDir = file('build/generated-snippets')
}

configurations {
    asciidoctorExt
}

dependencies {
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    implementation project(':foundation')

    implementation 'org.flywaydb:flyway-core:11.8.2'
    implementation 'org.flywaydb:flyway-mysql:11.8.2'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly('io.netty:netty-resolver-dns-native-macos:4.1.119.Final:osx-x86_64')
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    configurations "asciidoctorExt"
    inputs.dir snippetsDir
    dependsOn test
    baseDirFollowsSourceDir()
}

asciidoctorj {
    modules {
        diagram.use()
    }
}

bootJar {
    dependsOn asciidoctor

    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
}
